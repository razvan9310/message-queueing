set terminal png size 1192,550 font ",19"
set output "db-throughput-combined.png"
set xlabel "Time (s)"
set ylabel "Throughput (3 cp_seg)"
set y2label "Throughput (32 cp_seg)"
set xtics 200
set mytics 2
set ytics 500
set mytics 5
set y2tics 500
set my2tics 5
set ytics nomirror

stats "db-throughput-cps3.dat" every ::300::1500 using 1:2 prefix "cps3"
stats "db-throughput-cps32.dat" every ::300::1500 using 1:2 prefix "cps32"
set yrange[2300:2700]
set y2range[2300:2700]

set label at cps3_pos_max_y, cps3_max_y "" point pointtype 7 pointsize 2 front
set label at cps3_pos_max_y, cps3_max_y sprintf("Max TP (3cps) = %d", cps3_max_y) center front tc rgb "red" offset 0,0.25

set label at cps32_pos_max_y, cps32_max_y "" point pointtype 7 pointsize 2 front
set label at cps32_pos_max_y, cps32_max_y sprintf("Max TP (32cps) = %d", cps32_max_y) center front tc rgb "red" offset 0,0.25

set key font ",19"
set xtics font ",19"
set ytics font ",19"

set lmargin 9
set rmargin 8
set tmargin 3
set bmargin 3

set tics front
set tics scale 2
set border lw 2
set key outside horizontal

Measured(x) = x < 300 ? 0 : (x > 1500 ? 0 : 3000)
set style fill transparent solid 0.5 noborder
plot Measured(x) with filledcurves lt rgb "yellow", \
     "db-throughput-cps3.dat" using 1:2 with lines title "Throughput (3 cps)" lw 2 axes x1y1, \
     "db-throughput-cps32.dat" using 1:2 with lines title "Throughput (32 cps)" lw 2 axes x2y2
