set term png
set output "response-time.png"
set xlabel "Time (seconds)"
set ylabel "Response time (milliseconds)"
set yrange [:60]
set xtics 100
set mxtics 10
set ytics 10
set mytics 10
set key font ",19"
set xtics font ",19"
set ytics font ",19"
set terminal png size 1192,550 font ",19"

stats 'response-time.dat' prefix "A" noout

set arrow 1 from A_pos_min_y, graph 0.1 to A_pos_min_y, A_min_y fill
set arrow 2 from A_pos_max_y, graph 0.925 to A_pos_max_y, A_max_y fill
set label 1 at A_pos_min_y, graph 0.1 sprintf("Min = %f", A_min_y) center offset 0, -1
set label 2 at A_pos_max_y, graph 0.925 sprintf("Max = %f", A_max_y) center offset 0, 1

plot "response-time.dat" using 1:2 with lines title "Response time over time", \
     A_mean_y title sprintf("Mean = %f", A_mean_y)
