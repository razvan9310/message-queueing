set term png
set output "stability.png"
set xlabel "Time (s)"
set ylabel "Response time (ms)"
set y2label "Throughput (requests)"
set xtics 200
set ytics 10
set mytics 2
set y2tics 200
set my2tics 2
set ytics nomirror

stats "response-time.dat" every ::300::1500 using 1:2 prefix "RT"
stats "throughput.dat" every ::300::1500 using 1:2 prefix "TP"
set xrange [200:1600]
set yrange [0:20]
set y2range [2100:2700]

set label at RT_pos_min_y, RT_min_y "" point pointtype 7 pointsize 2 front
set label 3 at RT_pos_min_y, RT_min_y sprintf("Min RT = %f", RT_min_y) center front tc rgb "red" offset 0,0.5
# set label at RT_pos_max_y, RT_max_y "" point pointtype 7 pointsize 2 front
# set label 4 at RT_pos_max_y, RT_max_y - 2 sprintf("Max RT = %f", RT_max_y) center front tc rgb "red"

# set label at TP_pos_min_y, second TP_min_y "" point pointtype 7 pointsize 2 front
# set label 5 at TP_pos_min_y, second TP_min_y - 10 sprintf("Min TP = %d", TP_min_y) center front tc rgb "red"
set label at TP_pos_max_y, second TP_max_y "" point pointtype 7 pointsize 2 front
set label 6 at TP_pos_max_y, second TP_max_y sprintf("Max TP = %d", TP_max_y) center front tc rgb "red" offset 0,0.5

set key font ",19"
set xtics font ",19"
set ytics font ",19"
set terminal png size 1192,550 font ",19"

set lmargin 7
set rmargin 8
set tmargin 3
set bmargin 3
set tics front
set tics scale 2
set border lw 2
set key outside horizontal

Measured(x) = x < 300 ? 0 : (x > 1500 ? 0 : 100)
set style fill transparent solid 0.4 noborder
plot Measured(x) with filledcurves lt rgb "yellow" , \
     "response-time.dat" using 1:2 with lines title "Response time" lw 2 axes x1y1, \
     RT_mean_y title sprintf("Mean RT = %f", RT_mean_y) lw 2.5 axes x1y1, \
     "throughput.dat" using 1:2 with lines title "Throughput" lw 2 axes x1y2, \
     TP_mean_y title sprintf("Mean TP = %d", TP_mean_y) lw 2.5 axes x1y2

